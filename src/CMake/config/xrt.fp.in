# SPDX-License-Identifier: Apache-2.0
# Copyright (C) 2019-2021 Xilinx, Inc. All rights reserved.

# --------------------
# xrt-config.cmake
# --------------------
#
# @PROJECT_NAME@ cmake module.
# This module sets the following variables in your project::
#
#   @PROJECT_NAME@_FOUND              - true if @PROJECT_NAME@ and all required components found on the system
#   @PROJECT_NAME@_VERSION            - VERSION of this package in x.y.z format
#   @PROJECT_NAME@_CMAKE_DIR          - Directory where this cmake module was found
#   @PROJECT_NAME@_INCLUDE_DIRS       - Directory where @PROJECT_NAME@ headers are located.
#   @PROJECT_NAME@_LINK_DIRS          - Directory where @PROJECT_NAME@ link libraries are located.
#   @PROJECT_NAME@_CORE_LIBRARIES     - libraries to link against.
#   @PROJECT_NAME@_COREUTIL_LIBRARIES - libraries to link against.
#   @PROJECT_NAME@_OPENCL_LIBRARIES   - libraries to link against.
#   @PROJECT_NAME@_SWEMU_LIBRARIES    - libraries to link against.
#   @PROJECT_NAME@_HWEMU_LIBRARIES    - libraries to link against.
@PACKAGE_INIT@

set(@PROJECT_NAME@_INCLUDE_DIRS "${PACKAGE_PREFIX_DIR}/xrt/include")
set(@PROJECT_NAME@_LINK_DIRS "${PACKAGE_PREFIX_DIR}/xrt/lib")

set(@PROJECT_NAME@_VERSION @XRT_VERSION_STRING@)

if(EXISTS "${CMAKE_CURRENT_LIST_DIR}/xrt-build-tree-targets.cmake")
  include("${CMAKE_CURRENT_LIST_DIR}/xrt-build-tree-targets.cmake")
else()
  if(EXISTS "${CMAKE_CURRENT_LIST_DIR}/xrt-targets.cmake")
    include("${CMAKE_CURRENT_LIST_DIR}/xrt-targets.cmake")
  endif()
endif()

get_filename_component(@PROJECT_NAME@_CMAKE_DIR "${CMAKE_CURRENT_LIST_DIR}" ABSOLUTE)

set(@PROJECT_NAME@_CORE_LIBRARIES @PROJECT_NAME@::xrt_core @PROJECT_NAME@::xrt_coreutil)
set(@PROJECT_NAME@_COREUTIL_LIBRARIES @PROJECT_NAME@::xrt_coreutil)
set(@PROJECT_NAME@_OPENCL_LIBRARIES @PROJECT_NAME@::xilinxopencl @PROJECT_NAME@::xrt++)
set(@PROJECT_NAME@_SWEMU_LIBRARIES @PROJECT_NAME@::xrt_swemu)
set(@PROJECT_NAME@_HWEMU_LIBRARIES @PROJECT_NAME@::xrt_hwemu)

set(@PROJECT_NAME@_FOUND True)

message(STATUS "Found @PROJECT_NAME@: ${@PROJECT_NAME@_CMAKE_DIR} (found version \"${@PROJECT_NAME@_VERSION}\")")
